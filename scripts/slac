#!/bin/bash

FILE="$HOME/hd/data/$1"

gnuplot -p << EOF
stats '$FILE' using 2:6 nooutput
set terminal png enhanced background rgb '#F0F7D4' size 1200,600
set output "$HOME/hd/plots/$1.png"
set grid
set title "сън (в минути)" font ",12"
set key invert reverse spacing 0.85 box font ',9' autotitle columnhead
set tics font ",9"
set xtics rotate nomirror
set mytics 4
set ytics 0,60
set y2tics 0,60
set autoscale y
set style histogram rowstacked
set style data histograms
set style line 100 lt 1 lc rgb "black" lw 1
set style line 101 lt 0.5 lc rgb "gray" lw 1
set grid noxtics mytics ytics ls 100, ls 101
set boxwidth 0.5 relative
set style fill solid 0.50 border -1
set multiplot layout 1,2 title "Данни за $1" font ",12"
set tmargin 1.5

plot "$FILE" using 3:xticlabels(1) lc rgb 'yellow', "" using 2 lc rgb 'green', \
STATS_mean_x w l lc rgb 'orange'

set title "крачки" font ",12"
set ytics 1000
set y2tics 1000
set mytics 2.5
plot "$FILE" using 6:xticlabels(1) lc rgb 'green', STATS_mean_y w l lc rgb 'orange'
EOF

